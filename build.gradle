plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'antlr'
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(11) }
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

group = 'org.dxworks'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    // Core binding (bundles natives for Win/Linux/macOS)
    implementation 'io.github.bonede:tree-sitter:0.26.3'
  
    // Language grammars
    implementation 'io.github.bonede:tree-sitter-java:0.23.4'
    implementation 'io.github.bonede:tree-sitter-c-sharp:0.23.1'
    implementation 'io.github.bonede:tree-sitter-python:0.23.4'
    implementation 'io.github.bonede:tree-sitter-typescript:0.23.2'
    implementation 'io.github.bonede:tree-sitter-javascript:0.23.1'
    implementation 'io.github.bonede:tree-sitter-php:0.23.11'
    implementation 'io.github.bonede:tree-sitter-ruby:0.23.1'
    implementation 'io.github.bonede:tree-sitter-rust:0.23.1'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.21.0'
    implementation 'com.github.jsqlparser:jsqlparser:5.3'
    implementation 'org.antlr:antlr4-runtime:4.13.2'
    antlr 'org.antlr:antlr4:4.13.2'
    // DXWorks ignore library (loads .ignore patterns)
    implementation 'org.dxworks:dx-ignore:1.1.0-SNAPSHOT'
    
    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'
    testImplementation 'com.approvaltests:approvaltests:26.6.0'
}

application {
    mainClass = 'org.dxworks.codeframe.App'
}

import org.gradle.api.plugins.antlr.AntlrTask

// Configure ANTLR generation for all grammars in src/main/antlr
tasks.withType(AntlrTask).configureEach {
    // Generate visitor interfaces and set a shared Java package for generated sources
    arguments.clear()
    arguments += ['-visitor',
                  '-package', 'org.dxworks.codeframe.analyzer.sql.generated',
                  // Make sure grammar imports like `import PlSqlLexer;` can be resolved
                  // when the grammars live under src/main/antlr/plsql
                  '-lib', 'src/main/antlr']
}

tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }

// Produce a single, self-contained JAR named exactly 'codeframe.jar'
tasks.named('shadowJar') {
    archiveBaseName.set('codeframe')
    archiveVersion.set('')
    archiveClassifier.set('')
}
// Ensure 'build' produces the shadow JAR
tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}

// Configure all Test tasks to use JUnit Platform and run in parallel
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}