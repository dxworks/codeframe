plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'antlr'
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


group = 'org.dxworks'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // Core binding (bundles natives for Win/Linux/macOS)
    implementation 'io.github.bonede:tree-sitter:0.26.3'
  
    // Language grammars
    implementation 'io.github.bonede:tree-sitter-java:0.23.4'
    implementation 'io.github.bonede:tree-sitter-c-sharp:0.23.1'
    implementation 'io.github.bonede:tree-sitter-python:0.23.4'
    implementation 'io.github.bonede:tree-sitter-typescript:0.23.2'
    implementation 'io.github.bonede:tree-sitter-javascript:0.23.1'
    implementation 'io.github.bonede:tree-sitter-php:0.23.11'
    implementation 'io.github.bonede:tree-sitter-ruby:0.23.1'
    implementation 'io.github.bonede:tree-sitter-rust:0.23.1'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.21.0'
    implementation 'com.github.jsqlparser:jsqlparser:5.3'
    implementation 'org.antlr:antlr4-runtime:4.13.2'
    antlr 'org.antlr:antlr4:4.13.2'
    // DXWorks ignore library (loads .ignore patterns)
    implementation 'org.dxworks.utils:dx-ignore:1.1.0'
    
    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.2'
    testImplementation 'com.approvaltests:approvaltests:26.6.0'
}

application {
    mainClass = 'org.dxworks.codeframe.App'
}

import org.gradle.api.plugins.antlr.AntlrTask

task generateSqlGrammarSource(type: AntlrTask) {
    source = fileTree(dir: 'src/main/antlr', include: 'sql/**/*.g4')
    outputDirectory = file('build/generated-src/antlr/sql')
    arguments.clear()
    arguments += ['-visitor',
                  '-package', 'org.dxworks.codeframe.analyzer.sql.generated',
                  '-lib', 'src/main/antlr']
}

task generateCobolGrammarSource(type: AntlrTask) {
    source = fileTree(dir: 'src/main/antlr', include: 'cobol/**/*.g4')
    outputDirectory = file('build/generated-src/antlr/cobol')
    arguments.clear()
    arguments += ['-visitor',
                  '-package', 'org.dxworks.codeframe.analyzer.cobol.generated',
                  '-lib', 'src/main/antlr']
}

tasks.named('generateGrammarSource') {
    enabled = false
}

task generateAllGrammarSource {
    dependsOn generateSqlGrammarSource, generateCobolGrammarSource
}

sourceSets {
    main {
        java {
            srcDir 'build/generated-src/antlr/sql'
            srcDir 'build/generated-src/antlr/cobol'
        }
    }
}

tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }
tasks.named('compileJava') { dependsOn generateAllGrammarSource }

// Produce a single, self-contained JAR named exactly 'codeframe.jar'
tasks.named('shadowJar') {
    archiveBaseName.set('codeframe')
    archiveVersion.set('')
    archiveClassifier.set('')
}
// Ensure 'build' produces the shadow JAR
tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}

// Configure all Test tasks to use JUnit Platform and run in parallel
tasks.withType(Test).configureEach {
    useJUnitPlatform()
    dependsOn 'generateAllGrammarSource'

    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}